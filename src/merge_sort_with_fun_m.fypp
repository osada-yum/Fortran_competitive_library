#! Include this file with settting these values.
#! :set MERGE_SORT_ITEM_TYPES = ["integer", "integer"]
#! :set MERGE_SORT_ITEM_KINDS = ["int32", "int64"]
#! :set MERGE_SORT_USE_MODULES = []
#:set MERGE_SORT_ITEMS = [f"{type}({kind})" for (type, kind) in zip(MERGE_SORT_ITEM_TYPES, MERGE_SORT_ITEM_KINDS)]
#:set MERGE_SORT_NAMES = [f"merge_sort_with_fun_{kind}" for kind in MERGE_SORT_ITEM_KINDS]
$:add_module("merge_sort_with_fun_m")
module merge_sort_with_fun_m
  use, intrinsic :: iso_fortran_env
  !> maybe use module.
#:for modulename in MERGE_SORT_USE_MODULES
  use ${modulename}$
#:endfor
  implicit none
  private
  public :: merge_sort
  interface merge_sort
#:for name, item in zip(MERGE_SORT_NAMES, MERGE_SORT_ITEMS)
#:block ordering_block(typename = item)
     module procedure :: ${name}$
#:endblock ordering_block
#:endfor
  end interface merge_sort

#:for kind in MERGE_SORT_ITEM_KINDS
  interface
     pure function compare_${kind}$(v1, v2) result(res)
       $:import_kind(typenames = ["int32", kind])
       ${item}$, intent(in) :: v1, v2
       integer(int32) :: res
     end function compare_${kind}$
  end interface
#:endfor
contains
#:for name, kind, item in zip(MERGE_SORT_NAMES, MERGE_SORT_ITEM_KINDS, MERGE_SORT_ITEMS)
#:block ordering_block(typename = item)
  pure subroutine ${name}$(arr, compare, indices)
    ${item}$, intent(inout) :: arr(:)
    integer(int32), intent(inout), optional :: indices(:)
    procedure(compare_${kind}$) :: compare
    integer(int32), allocatable :: idx(:)
    integer(int32) :: n
    integer(int32) :: i
    n = size(arr)
    allocate(idx, source = [(i, i = 1, n)])
    call merge_sort_with_fun_sub(n, arr, idx, compare)
    if (present(indices)) &
         indices(:) = idx(:)
    return
  contains
    pure subroutine merge_sort_with_fun_sub(n, arr, idx, compare)
      integer(int32), intent(in) :: n
      ${item}$, intent(inout) :: arr(n)
      integer(int32), intent(inout) :: idx(n)
      procedure(compare_${kind}$) :: compare
      integer(int32) :: width
      integer(int32) :: i, l, r
      width = 2
      do while (width <= 2 * n)
         do i = 1, n, width
            if (i + width / 2 > n) exit
            l = i
            r = min(n, i + width - 1)
            call ${name}$_merge_with_key(r - l + 1, width/2, arr(l:r), idx(l:r), compare)
         end do
         width = width * 2
      end do
    end subroutine merge_sort_with_fun_sub
  end subroutine ${name}$
  pure subroutine ${name}$_merge_with_key(n, nl, arr, indices, compare)
    integer(int32), intent(in) :: n, nl
    ${item}$, intent(inout) :: arr(n)
    integer(int32), intent(inout) :: indices(n)
    procedure(compare_${kind}$) :: compare
    integer(int32) :: idx_left(1:nl), idx_right(nl + 1:n)
    integer(int32) :: idx(n)
    integer(int32) :: i, j, k
    if (n == 1) return
    idx_left(1:nl) = [(i, i = 1, nl)]
    idx_right(nl + 1:n) = [(i, i = nl + 1, n)]
    i = 1
    j = nl + 1
    do k = 1, n
       if (compare(arr(i), arr(j)) <= 0) then
          idx(k) = idx_left(i)
          i = i + 1
       else
          idx(k) = idx_right(j)
          j = j + 1
       end if
       if (i > nl) then
          idx(k + 1:n) = idx_right(j:n)
          exit
       else if (j > n) then
          idx(k + 1:n) = idx_left(i:nl)
          exit
       end if
    end do
    arr(:) = arr(idx)
    indices(:) = indices(idx)
  end subroutine ${name}$_merge_with_key
#:endblock ordering_block
#:endfor
end module merge_sort_with_fun_m
