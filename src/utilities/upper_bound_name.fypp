#:mute

#:def upper_bound_name(type, kind = "")
upper_bound_${type}$_${kind}$
#:enddef upper_bound_name

#:def upper_bound(type, kind = "")
#:set item = f"{type}({kind})" if kind != "" else type
#:set name = upper_bound_name(type = type, kind = kind)
  !> ${name}$: Return the index of the leftmost position that satisfies `arr(res) > key` in the sorted array.
  !> @param arr A sorted array.
  !> @param key A value.
  pure integer(int32) function ${name}$(arr, key) result(res)
    ${item}$, intent(in) :: arr(:)
    ${item}$, intent(in) :: key
    integer(int32) :: p, q, r
    p = 1
    r = size(arr)
    if (r == 0) then
       res = 1; return
    else if (arr(p) > key) then
       res = 1; return
    else if (arr(r) <= key) then
       res = r + 1; return
    end if
    !> invariant condition:
    !> arr(p) <= key < arr(r)
    binary_search: do while(p + 1 < r)
       q = p + (r - p) / 2
       if (arr(q) <= key) then
          p = q
       else !> key < arr(q)
          r = q
       end if
    end do binary_search
    res = r
  end function ${name}$
#:enddef
#:endmute
